module Implicitize

# Based on:
#   T.W. Sederberg, D.C. Anderson, R.N. Goldman:
#     Implicit representation of parametric curves and surfaces
#       Computer Vision, Graphics, and Image Processing 28, pp. 72-84, 1984.

# Usage example:
# [Julia]
#   julia> Implicitize.write(2, 1, "test.mac");
# [Maxima]
#   (%i1) load("test.mac")$
#   (%i2) res;

# Programming-friendly output can be generated by issuing the command
#   (%i3) display2d: false$
#   (%i4) res;

# Generate Maxima code for the computation

function A(n, m, i, j, k, l)
    println("A[$i,$j,$k,$l]: 0\$")
    for I in 0:i, K in 0:i
        M1 = i - I - K
        M = M1 + k + 1
        if M1 >= 0 && I <= n && K <= n && M <= n
            for J in 0:l, N in 0:l
                L1 = l - J - N
                L = L1 + j + 1
                if L1 >= 0 && J <= m && L <= m && N <= m
                    println("m33: matrix([a[$I,$J],a[$K,$L],a[$M,$N]]," *
                                        "[b[$I,$J],b[$K,$L],b[$M,$N]]," *
                                        "[c[$I,$J],c[$K,$L],c[$M,$N]])\$")
                    println("A[$i,$j,$k,$l]: A[$i,$j,$k,$l] + determinant(m33)\$")
                end
            end
        end
    end
end

function resultant(n, m)
    for i in 0:2n-1, j in 0:m-1
        for k in 0:n-1, l in 0:2m-1
            A(n, m, i, j, k, l)
        end
    end
    print("mat: matrix(")
    for i in 0:2n-1, j in 0:m-1
        print("[")
        for k in 0:n-1, l in 0:2m-1
            print("A[$i,$j,$k,$l]")
            (k < n-1 || l < 2m-1) && print(",")
        end
        print("]")
        (i < 2n-1 || j < m-1) && print(",")
    end
    println(")\$")
    println("res: determinant(mat)\$")
end

function bezier(n, m)
    println("B(n,k,t) := binomial(n,k) * t^k * (1-t)^(n-k)\$")
    for (c, a) in [("x", "a"), ("y", "b"), ("z", "c")]
        print("surf$c: ")
        for i in 0:n, j in 0:m
            print("P$c[$i,$j] * B($n,$i,s) * B($m,$j,t)")
            (i < n || j < m) && print(" + ")
        end
        println("\$")
        for i in 0:n, j in 0:m
            print("$a[$i,$j]: ratcoef(ratcoef(surf$c, s, $i), t, $j)")
            i == 0 && j == 0 && print(" - $c")
            println("\$")
        end
    end
end

function write(n, m, filename)
    open(filename, "w") do f
        redirect_stdout(() -> (bezier(n, m); resultant(n, m)), f)
    end
end

end
